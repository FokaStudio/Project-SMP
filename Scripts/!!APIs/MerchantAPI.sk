#  __  __               _                 _            _____ _____ 
# |  \/  |             | |               | |     /\   |  __ \_   _|
# | \  / | ___ _ __ ___| |__   __ _ _ __ | |_   /  \  | |__) || |  
# | |\/| |/ _ \ '__/ __| '_ \ / _` | '_ \| __| / /\ \ |  ___/ | |  
# | |  | |  __/ | | (__| | | | (_| | | | | |_ / ____ \| |    _| |_ 
# |_|  |_|\___|_|  \___|_| |_|\__,_|_| |_|\__/_/    \_\_|   |_____|
#                                                                  
#                                                                  
#   Original by Tuke_Nuke (https://forums.skunity.com/resources/villagerapi-changes-the-trades-of-your-villager.41/)
#   On my server I often use Villagers for stuff like shops or even custom GUIs other than chests,
#   so it was pretty sad to see addons like MISK to be left behind (I can't use Khoryl, so it was kinda a problem).
#   But then I have stumbled upon Tuke_Nuke's API and decided to remake it!
#   MerchantAPI is great succesor for that script! Kinda tricky and hard to use because of my poort scripting knowledge,
#   but it can easily solve all the problems with Villagers!
#   It's NBT driven, so it should work in every version above 1.14 (if SkBee is working on that version)
#
#  _____                  _                               _       
# |  __ \                (_)                             | |      
# | |__) |___  __ _ _   _ _ _ __ ___ _ __ ___   ___ _ __ | |_ ___ 
# |  _  // _ \/ _` | | | | | '__/ _ \ '_ ` _ \ / _ \ '_ \| __/ __|
# | | \ \  __/ (_| | |_| | | | |  __/ | | | | |  __/ | | | |_\__ \
# |_|  \_\___|\__, |\__,_|_|_|  \___|_| |_| |_|\___|_| |_|\__|___/
#                | |                                              
#                |_|                                              
#
#! Skript (ofc, tested on 2.5.3)
#! SkBee (tested on 1.10.2)
#
#   _____             _             
#  / ____|           | |            
# | (___  _   _ _ __ | |_ __ ___  __
#  \___ \| | | | '_ \| __/ _` \ \/ /
#  ____) | |_| | | | | || (_| |>  < 
# |_____/ \__, |_| |_|\__\__,_/_/\_\
#          __/ |                    
#         |___/                     
#
#!!     MerchantTrade: 
#           DESCRIPTION: Used to add trades to a villager, requires to be added to a list! Also, if you want to make a trade with only 1 item to pay, use air in the second part.
#!          USAGE: MerchantTrade(item A (item), Count A (number), item B (item), Count B (number), Result Item (item), Result Count (number), Max Uses (number), Experience Reward (boolean))
#!!         EXAMPLE: add MerchantTrade(stick named "bruh", 1, sponge with lore "&6Example lore", 7, emerald, 3, 5, true) to {_trades::*}
#  
#!!     changeMerchantProperties:
#           DESCRIPTION: Changes properties of a Villager, like type (savanna, plains, etc.), job (e.g. cleric, farmer) and level (novice, expert, journeyman...)
#!          USAGE: changeMerchantProperties(merchant (entity), type (text*), job (text**), level (number***))
#!!         EXAMPLE:
#!!              on rigtclick with stick:
#!!                   spawn villager at target block
#!!                   add {NoAI:1b} to the nbt of last spawned villager
#!!                   changeMerchantProperties(last spawned villager, jungle, farmer, 5)
#
#       *Accepted values: "savanna", "snowy", "swamp", "plains", "taiga", "desert", "jungle"
#       **Accepted values: "farmer", "cleric", "armorer", "butcher", "weaponsmith", "shepherd", 
#         "cartographer", "fisherman", "nitwit", "mason", "leatherworker", "fletcher", 
#         "toolsmith", "librarian"
#       ***Accepted values: 1, 2, 3, 4, 5, 99
#
#!  _____ __  __ _____   ____  _____ _______       _   _ _______ _ 
#! |_   _|  \/  |  __ \ / __ \|  __ \__   __|/\   | \ | |__   __| |
#!   | | | \  / | |__) | |  | | |__) | | |  /  \  |  \| |  | |  | |
#!   | | | |\/| |  ___/| |  | |  _  /  | | / /\ \ | . ` |  | |  | |
#!  _| |_| |  | | |    | |__| | | \ \  | |/ ____ \| |\  |  | |  |_|
#! |_____|_|  |_|_|     \____/|_|  \_\ |_/_/    \_\_| \_|  |_|  (_)
#
#   This API is tricky to use due to how it's created!
#   When adding trades to a Merchant you need to put all trades in a list!
#   When you have added all your recipes, keep in mind that they REPLACE EXISTING ONES,
#   they don't add them next to them!
#   Also, in order to add them, you have to first connect the trades in the list
#   AND ADD THEM AS NBT TAG! They get automatically converted to nbt string when put in list,
#   so don't worry about it!
#
#
#!  EXAMPLE JOIN CODE:
#!      set {_trades} to (join {_r::*} with ",")
#!      add "{Offers:{Recipes:[%{_trades}%]}}" to the nbt of {_e}
#!
#!          YOU ALWAYS HAVE TO JOIN WITH COMA (,) OR IT WILL FAIL TO WORK, BECAUSE IT'S NBT
#
#
#
#
#!!         EXAMPLE CODE:
#!!             command /merchantapitest:
#!!                 trigger:
#!!                     summon villager at player
#!!                     add MerchantTrade(stick, 1, flint, 1, arrow, 4, 16, true) to {_trades::*}
#!!                     add MerchantTrade(diamond shovel named "&6Super Shovel", 1, netherite ingot, 3, netherite shovel named "&8Netherite Super Shovel", 1, 99999, false) to {_trades::*} 
#!!                     add MerchantTrade(iron ingot, 1, yellow dye, 1, gold ingot, 2, 1, false) to {_trades::*}
#!!                     set {_trades} to (join {_trades::*} with ",")
#!!                     add "{Offers:{Recipes:[%{_trades}%]}}" to the nbt of last spawned villager
#!!
#
#
#   ____  _   _               
#  / __ \| | | |              
# | |  | | |_| |__   ___ _ __ 
# | |  | | __| '_ \ / _ \ '__|
# | |__| | |_| | | |  __/ |   
#  \____/ \__|_| |_|\___|_|   
#                             
#   Bugs? Suggestions? Feedback?                             
#   Ping me in SkUnity's Discord or DM me! (FokaStudio ãƒ„#7055)
#
#   Thanks for choosing MerchantAPI!
#
function MerchantTrade(trade1: item, trade1count: number, trade2: item, trade2count: number, result: item, resultcount: number, maxUses: number, expreward: boolean) :: text:
    set {_i1} to minecraft name of {_trade1}
    set {_i2} to minecraft name of {_trade2}
    set {_i3} to minecraft name of {_result}
    set {_i1nbt} to nbt of {_trade1}
    set {_i2nbt} to nbt of {_trade2}
    set {_i3nbt} to nbt of {_result}
    if {_expreward} = true:
        set {_rxp} to 1
    else:
        set {_rxp} to 0
    return "{maxUses:%{_maxuses}%,rewardExp:%{_rxp}%,buy:{id:""%{_i1}%"",Count:%{_trade1count}%,tag:%{_i1nbt}%},buyB:{id:""%{_i2}%"",Count:%{_trade2count}%,tag:%{_i2nbt}%},sell:{id:""%{_i3}%"",Count:%{_resultcount}%,tag:%{_i3nbt}%}}"

function MerchantTrade2(trade: item, tradecount: number, result: item, resultcount: number, maxUses: number, expreward: boolean) :: text:
    set {_i1} to minecraft name of {_trade}
    set {_i3} to minecraft name of {_result}
    set {_i1nbt} to nbt of {_trade}
    set {_i3nbt} to nbt of {_result}
    if {_expreward} = true:
        set {_rxp} to 1
    else:
        set {_rxp} to 0
    return "{maxUses:%{_maxuses}%,rewardExp:%{_rxp}%,buy:{id:""%{_i1}%"",Count:%{_tradecount}%,tag:%{_i1nbt}%},buyB:{id:""minecraft:air"",Count:1b},sell:{id:""%{_i3}%"",Count:%{_resultcount}%,tag:%{_i3nbt}%}}"

effect open merchant %living entity% to %players%:
    trigger:
        expr-2.openMerchant(expr-1, true)

function changeMerchantProperties(entity: entity, biome: text, job: text, level: number):
    add "{VillagerData:{""type"":""%{_biome}%"",""profession"":""%{_job}%"",""level"":""%{_level}%""}}" to the nbt of {_entity}