on right click on iron golem:
	if player's tool is iron block:
		if player has 6 of iron block:
			if event-entity doesn't have scoreboard tag "reinf.golem":
				remove 6 of player's tool from player
				add "reinf.golem" to the scoreboard tags of event-entity
				play huge explosion at event-entity
				set max health of event-entity to 100
				heal event-entity
				set name of event-entity to "<##20D600>Reinforced Golem"
				apply potion of strength of tier 2 without any particles to event-entity for 999 days
				apply potion of resistance of tier 1 without any particles to event-entity for 999 days
				play sound "block.anvil.use" with volume 1 and pitch 0.5 for all players at event-entity
				play sound "psmp:utility.iron_golem.spawn" with volume 1 and pitch 0.8 for all players at event-entity
				
			else:
				send action bar "&cThis Iron Golem is already reinforced!" to player
		else:
			send action bar "&cYou need 6 Iron Blocks to reinforce Iron Golems!" to player

every 5 ticks:
	loop all iron golems where [input has scoreboard tag "reinf.golem"]:
		ParticleAPI 7 end_rod at location 1.5 meters above loop-value offset vector(0.7, 0.7, 0.7) extra 0 for all players
	loop all snow golems:
		ParticleAPI 15 falling_dust using snow_block at location 4 meters above loop-value offset vector(.75, 0.2, .75) extra .1 for all players

on damage of snow golem:
	if attacker is snow golem:
		cancel event

on death of iron golem:
	victim has scoreboard tag "reinf.golem"
	drop (random integer between 2 and 5) of iron block

on spawn of snow golem:
	apply potion of slowness of tier 8 without any particles to event-entity for 999 days
