# Copyright 2021  FokaStudio

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.


on spawn of zombie or husk or spider or cave spider or skeleton or stray:
	spawn reason is natural or spawn egg
	make event-entity invulnerable
	spawn armor stand at event-entity with nbt "{Invisible:1b}"
	set gravity of event-entity to false
	set {_a} to last spawned armor stand
	teleport event-entity to location 2.5 meters under event-entity
	set {_times} to 0
	while block at event-entity is not passable:
		add 1 to {_times}
		teleport event-entity to location 0.03 meter upwards
		play 20 of block particle using (type of block at location 0.5 meter above event-entity) at {_a}
		if {_times} = 5:
			set {_breakSound} to (block at event-entity).getSoundGroup().getBreakSound()
			(event-entity's world).playSound(event-entity.getLocation(), {_breakSound}, 1, 0.75)
			set {_times} to 0
		wait 1 tick
	make event-entity vulnerable
	set gravity of event-entity to true
	kill {_a}

on spawn of creeper:
	spawn reason is natural or spawn egg
	show 25 large explosion at location 1 meter above event-entity
	play sound "entity.generic.explode" with volume 1 and pitch 2 at event-entity for all players

on spawn of enderman:
	spawn reason is natural or spawn egg
	set {_a} to location 1.5 meter above event-entity
	wait 1 tick
	make event-entity invulnerable
	set ai of event-entity to false
	apply potion of invisibility without any particles to event-entity for 195 ticks
	loop 10 times:
		play ender signal at {_a}
		play sound "entity.enderman.teleport" with volume 1 and pitch 1.15 at {_a} for all players
		wait 10 ticks
	loop 10 times:
		play ender signal at {_a}
		play sound "entity.enderman.teleport" with volume 1 and pitch 1.15 at {_a} for all players
		wait 5 ticks
	loop 10 times:
		play ender signal at {_a}
		play sound "entity.enderman.teleport" with volume 1 and pitch 1.15 at {_a} for all players
		wait 3 ticks
	wait 15 ticks
	play sound "entity.enderman.teleport" with volume 1 and pitch 0.75 at {_a} for all players
	play flash at {_a}
	make event-entity visible
	make event-entity vulnerable
	set ai of event-entity to true