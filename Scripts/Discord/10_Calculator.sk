# > Made by Sky with <3 (and updated by FokaStudio)
# > Need at least a stable version of DiSky v2.0

# > It use a libs to parse expression into math expression:
# > exp4j: https://github.com/fasseg/exp4j

# > You can edit it as you want, but please credit me for the basics <3

options:
	version: 1.0-FOKAPATCH

# > Main command
discord command calculator:
	prefixes: !
	aliases: calc
	category: general
	usage: !calculator
	description: A simple calculator
	trigger:
		make embed:
			set author of embed to "Loading calculator ..."
			set author icon of embed to "https://zupimages.net/up/21/22/grmo.png"
			set color of embed to orange
			set footer of embed to "It can take several seconds..."
		reply with last embed and store it in {_msg}
		setupButtons({_msg}, event-member)
		wait 3 seconds
		edit {_msg} to show getMemberCalc(event-member) and keep buttons

# > Buttons Execution
on button click:
	
	set {_id} to discord id of event-button
	set {_pars::*} to split {_id} at ":"
	{_pars::1} is "calc"

	defer the interaction
	
	set {_m} to member with id {_pars::2} in event-guild
	set {_id} to discord id of {_m}
	
	discord id of {_m} is discord id of event-member
	
	if size of (split {_pars::3} at "") is smaller than 3:
		{CALC::%{_id}%::OPERATION} doesn't contain "ERROR"
		set {_current} to {CALC::%{_id}%::OPERATION}
		set {_size} to size of (split {_current} at "")
		set {_last} to ({_size} - 1)st element of (split {_current} at "")
		set {_next} to {_pars::3}

		if {_next} is "1", "2", "3", "4", "5", "6", "7", "8", "9", "0" or "00":
			if {_last} = "0":
				{_size} = 2
				set {CALC::%{_id}%::OPERATION} to "%{_next}%"
			else:
				set {CALC::%{_id}%::OPERATION} to "%{_current}%%{_next}%"
		else if {_next} is ".":
			if {_size} = 2:
				set {CALC::%{_id}%::OPERATION} to "0"
			else:
				set {CALC::%{_id}%::OPERATION} to "%{_current}%%{_next}%"
		else if {_next} is "*", "/", "+" or "-":
			{_last} is not "*", "/", "+" or "-"
			set {CALC::%{_id}%::OPERATION} to "%{_current}%%{_next}%"

		edit {CALC::%{_id}%::MSG} to show getMemberCalc({_m}) and keep buttons
	else:
		set {_action} to {_pars::3}
		if {_action} is "remove":
			{CALC::%{_id}%::OPERATION} doesn't contain "ERROR"
			set {_current} to {CALC::%{_id}%::OPERATION}
			set {_size} to size of (split {_current} at "")
			if {_size} = 2:
				set {CALC::%{_id}%::OPERATION} to "0"
				edit {CALC::%{_id}%::MSG} to show getMemberCalc({_m}) and keep buttons
				stop
			set {_current} to {CALC::%{_id}%::OPERATION}
			set {_cs::*} to split {_current} at ""
			set {_size} to (size of {_cs::*}) - 1
			delete {_cs::%{_size}%}
			set {_current} to join {_cs::*} with ""
			set {CALC::%{_id}%::OPERATION} to {_current}
			edit {CALC::%{_id}%::MSG} to show getMemberCalc({_m}) and keep buttons
		else if {_action} is "clear":
			set {CALC::%{_id}%::OPERATION} to "0"
			edit {CALC::%{_id}%::MSG} to show getMemberCalc({_m}) and keep buttons
		else if {_action} is "result":
			{CALC::%{_id}%::OPERATION} doesn't contain "ERROR"
			set {CALC::%{_id}%::OPERATION} to eval({CALC::%{_id}%::OPERATION}, {_m})
			set {CALC::%{_id}%::LASTERROR} to "%last java error%"
			edit {CALC::%{_id}%::MSG} to show getMemberCalc({_m}) and keep buttons
		else if {_action} is "exit":
			set {CALC::%{_id}%::OPERATION} to "0"
			delete discord entity event-message
			reply with "**:wave: Goodbye %mention tag of event-user% !**"
		else if {_action} is "disky":
			make embed:
				set title of embed to "Calculator v{@version}"
				add "This calculator was developed by ItsTheSky, and is free to download in %mention tag of channel with id ""818225595284455464""%." to {_l::*}
				add "You must have DiSky v2.0 or higher to run it!" to {_l::*}
				add "" to {_l::*}
				add "This program uses a Java library to parse mathematical expressions, available [HERE](https://github.com/fasseg/exp4j)." to {_l::*}
				add "Don't hesitate to mention me in case of bugs :)" to {_l::*}
				set description of embed to join {_l::*} with nl
				set color of embed to lime
				set footer of embed to "Asked by %discord name of event-user%"
			reply with last embed

# > Buttons Adding
function setupButtons(msg: message, m: member):

	set {_id} to discord id of {_m}
	if {CALC::%{_id}%::MSG} is set:
		delete discord entity {CALC::%{_id}%::MSG}
	set {CALC::%{_id}%::MSG} to {_msg}

	set {_row} to new buttons row
	add new button with url "calc:%{_id}%:1" with style secondary with content "1" to buttons of {_row}
	add new button with url "calc:%{_id}%:2" with style secondary with content "2" to buttons of {_row}
	add new button with url "calc:%{_id}%:3" with style secondary with content "3" to buttons of {_row}
	add new button with url "calc:%{_id}%:*" with style primary with content "x" to buttons of {_row}
	add new button with url "calc:%{_id}%:exit" with style danger with content "Exit" to buttons of {_row}

	set {_row2} to new buttons row
	add new button with url "calc:%{_id}%:4" with style secondary with content "4" to buttons of {_row2}
	add new button with url "calc:%{_id}%:5" with style secondary with content "5" to buttons of {_row2}
	add new button with url "calc:%{_id}%:6" with style secondary with content "6" to buttons of {_row2}
	add new button with url "calc:%{_id}%:/" with style primary with content "÷" to buttons of {_row2}
	add new button with url "calc:%{_id}%:remove" with style danger with content "←" to buttons of {_row2}

	set {_row3} to new buttons row
	add new button with url "calc:%{_id}%:7" with style secondary with content "7" to buttons of {_row3}
	add new button with url "calc:%{_id}%:8" with style secondary with content "8" to buttons of {_row3}
	add new button with url "calc:%{_id}%:9" with style secondary with content "9" to buttons of {_row3}
	add new button with url "calc:%{_id}%:+" with style primary with content "+" to buttons of {_row3}
	add new button with url "calc:%{_id}%:clear" with style danger with content "Clear" to buttons of {_row3}

	set {_row4} to new buttons row
	add new button with url "calc:%{_id}%:disky" with style primary with content "?" to buttons of {_row4}
	add new button with url "calc:%{_id}%:0" with style secondary with content "0" to buttons of {_row4}
	add new button with url "calc:%{_id}%:." with style primary with content "." to buttons of {_row4}
	add new button with url "calc:%{_id}%:-" with style primary with content "-" to buttons of {_row4}
	add new button with url "calc:%{_id}%:result" with style success with content "=" to buttons of {_row4}
	
	add row {_row}, {_row2}, {_row3} and {_row4} to message {_msg}


# > Calculation
import:
	net.objecthunter.exp4j.ExpressionBuilder
	net.objecthunter.exp4j.Expression

function eval(input: text, m: member) :: text:
	set {_id} to discord id of {_m}
	set {_n} to try new ExpressionBuilder({_input}).build().evaluate()
	if "%last java throwable%" contains "java.lang.IllegalArgumentException":
		set {CALC::%{_id}%::LASTGOODEXPR} to "%{_input}%"
		return "ERROR (Clear to refresh)"
	return "%{_n}%"

# > Visual Embed
function getMemberCalc(m: member) :: embed:
	set {_id} to discord id of {_m}
	set {_current} to {CALC::%{_id}%::OPERATION}
	if {_current} is not set:
		set {_current} to "0"
		set {CALC::%{_id}%::OPERATION} to "0"
	
	if {_current} contains "ERROR":
		set {_err} to {CALC::%{_id}%::LASTERROR}
		replace all "java.lang.IllegalArgumentException: " with "" in {_err}
		make embed:
			set description of embed to "```%{_err}%```%nl%**Your Expression was ```%{CALC::%{_id}%::LASTGOODEXPR}%```**"
			set color of embed to dark red
			set author of embed to "An Math error occured. Full Message:"
			set author icon of embed to "https://zupimages.net/up/21/21/8oif.png"
			set footer of embed to "Use the 'Clear' button to reset the calculator"
	else:
		make embed:
			set title of embed to "%discord name of {_m}%'s Calculator"
			set description of embed to "```%{_current}%```"
			set color of embed to color from hex "36393f"
	
	return last embed