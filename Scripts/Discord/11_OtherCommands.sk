# Copyright 2021  FokaStudio

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.


# Coded by: FokaStudio
# Original by: <none>
# Addons required: DiSky, Reqn, skript-json
# Other plugins: <none>


discord command avatar [<member=%event-member%>]:
	prefixes: !
	aliases: av, a
	executable in: guild
	bots: Butler
	usage: !avatar [<member>]
	category: general
	description: Grab the Avatar of any member of this guild
	trigger:
		set {_red} to random integer between 1 to 255
		set {_green} to random integer between 1 to 255
		set {_blue} to random integer between 1 to 255
		make embed:
			set title of embed to "Avatar of %discord name of arg-1%"
			set colour of the embed to color from rgb {_red}, {_green}, {_blue}
			set description of embed to "**Avatar of %discord name of arg-1%##%tag of arg-1%**"
			set image of embed to avatar of arg-1
			set footer of embed to "Requested by %discord name of event-user%##%tag of event-user%"
			set timestamp of embed to now
		reply to event-message with last embed

discord command clyde [<text>]:
	prefixes: !
	executable in: guild
	bots: Butler
	category: general
	description: Make my good friend Clyde say something
	trigger:
		if arg-1 is not set:
			reply to event-message with ":x: **Maybe you should include smth in 1st argument?**"
			stop
		set {_arg1} to arg-1
		replace all " " with "%%20" in {_arg1}
		reply to event-message with "https://ctk-api.herokuapp.com/clyde/%{_arg1}%"

discord command whois [<member>]:
	prefixes: !
	aliases: userinfo
	category: general
	description: Grab info of one of the members of our server!
	trigger:
		if arg 1 is not set:
			reply to event-message with ":x: You **must** specify a member !"
			stop
		set {_uuid} to yaml value "links.%id of arg-1%" from "data"
		make embed:
			set title of embed to "Who is %discord name of arg-1% ?"
			add "`•` Username: %discord name of arg 1%" to {_l::*}
			add "`•` Nick: %discord nickname of arg 1%" to {_l::*}
			add "`•` Tag: %tag of arg 1%" to {_l::*}
			add "`•` Mention: %mention tag of arg 1%" to {_l::*}
			add "`•` Linked: %{-uuidnamecache::%{_uuid}%}%" to {_l::*}
			add "`•` Mutual Guilds: %size of mutual guild of arg 1%" to {_l::*}
			set description of embed to join {_l::*} with nl
			set thumbnail of embed to avatar of arg-1
			set footer of embed to "Asked by %discord name of event-member%"
			set color of embed to color of arg-1
			set timestamp of embed to now
		reply to event-message with last embed

discord command server [<text>]:
	prefixes: !
	executable in: guild
	bots: Butler
	category: minecraft
	usage: !server <minecraft server>
	aliases: mcserv, mcserver, serv, serverinfo
	description: Grab all info about any server in a form of a neat embed
	trigger:
		if arg-1 is not set:
			reply to event-message with ":x: **You really should specify 1st argument...**%nl%*EXAMPLE:* ```!mcserv mc.hypixel.net```"
		send http request to "https://api.mcsrvstat.us/2/%arg-1%"
		set {_server} to last http response
		set {_server} to {_server}'s body
		map json {_server} to {_server::*}
		if {_server::error} is set:
			reply to event-message with ":x: **There isn't any server with this IP, try one that actually exist...**"
			stop
		if {_server::online} = true:
			make embed:
				set color of the embed to light red
				set title of embed to "All info about %arg-1%"
				set description of the embed to "Not sure what Protocol is? Click on the title!"
				add inline field named ":green_circle: Server Status" with value "Online" to fields of embed
				add inline field named ":pushpin: Online" with value "%{_server::players::online}% / %{_server::players::max}%" to fields of embed
				add inline field named ":compass: Version" with value "%{_server::version}%" to fields of embed
				add inline field named ":lock: Protocol" with value "%{_server::protocol}%" to fields of embed
				add inline field named "%reaction "":776710047903383562:""% IP" with value "%{_server::ip}%" to fields of embed
				add inline field named "%reaction "":776710041477578772:""% Port" with value "%{_server::port}%" to fields of embed
				add inline field named ":page_facing_up: MOTD" with value "```%{_server::motd::clean::1}%%nl%%{_server::motd::clean::2}%```" to fields of embed
				set the thumbnail of embed to "https://api.minetools.eu/favicon/%arg-1%"
				set title url of embed to "https://wiki.vg/Protocol_version_numbers"
				set the footer of embed to "Requested by %event-user%##%tag of event-user%"
				set the timestamp of embed to now		
			reply to event-message with last embed
		else:
			make embed:
				set color of the embed to dark red
				set title of embed to "%arg-1% is currently offline"
				set description of embed to "or I am unable to reach it :(%nl%Try searching for the server that is currently online, like **Hypixel**, which is never down"
				set the footer of embed to "Requested by %event-user%##%tag of event-user%"
				set the timestamp of embed to now		
			reply to event-message with last embed

discord command search [<text>]:
	prefixes: !
	executable in: guild
	bots: Butler
	category: general
	usage: !search <query>
	description: Search the web for anything you want 
	trigger:
		if arg-1 is not set:
			reply to event-message with ":x: **Hey, did you forgot about 1st argument again?**"
			stop
		reply to event-message with "**Here is all that I found when searching for *%arg-1%***"
		set {_arg} to arg-1
		replace all " " in {_arg} with "%%20"
		reply with "https://image.thum.io/get/auth/40904-Butler/nomention/https://google.com/search?q=%{_arg}%"

discord command player [<text>]:
	prefixes: !
	executable in: guild
	aliases: mcplayer, playerinfo
	usage: !player <player>
	category: minecraft
	bots: Butler
	description: Grab any info about a specified player
	trigger:
		if arg-1 is not set:
			reply to event-message with ":x: **Do you really hate that 1st argument? SPECIFY THE PLAYER**"
			stop
		send http request to "https://api.minetools.eu/uuid/%arg-1%"
		set {_player} to last http response
		set {_player} to {_player}'s body
		map json {_player} to {_player::*}
		if {_player::error} is set:
			reply to event-message with ":x: **This player doesn't exist**"
			stop
		send http request to "https://api.minetools.eu/profile/%{_player::id}%"
		set {_playerinfo} to last http response
		set {_playerinfo} to {_playerinfo}'s body
		map json {_playerinfo} to {_playerinfo::*}
		wait 2 ticks
		if {_playerinfo::decoded::textures::SKIN::metadata::model} is set:
			set {_model} to "slim" 
		else: 
			set {_model} to "normal"
		make embed:
			set color of the embed to light red
			set title of embed to "All info about %arg-1%"
			set description of the embed to "Click on the title to go to %arg-1%'s NameMC profile"
			add inline field named ":id: Profile ID" with value "%{_playerinfo::decoded::profileId}%" to fields of embed
			add inline field named ":name_badge: Profile Name" with value "%{_playerinfo::decoded::profileName}%" to fields of embed
			add inline field named ":person_standing: Player Model" with value "%{_model} in proper case%" to fields of embed
			set the thumbnail of embed to "https://visage.surgeplay.com/bust/%{_playerinfo::decoded::profileId}%"
			set title url of embed to "https://namemc.com/profile/%{_playerinfo::decoded::profileName}%"
			set the footer of embed to "Requested by %event-user%##%tag of event-user%"
			set the timestamp of embed to now		
		reply to event-message with last embed
