discord command tagcreator [<text>] [<text>] [<text>]:
	aliases: tcreator, tc, tagmanager, tm, tmanager, tagc, tagm
	permissions: administrator
	prefixes: !
	trigger:
		if arg-1 is "help":
			reply to event-message with "**TAG MANAGER**%nl%*Version 1.0, by FokaStudio, for FokaStudio <3*%nl%%nl%**USAGE**``!(t[ag]c[reator]|t[ag]m[anager]) (create|edit|delete|remove|image|extra|thumbnail) %%tag's id%% [%%text, depends on arg-2%%]``%nl%%nl%***INFO***```create - registers the Tag with specified ID, 3rd argument should be used as 'title|description'%nl%%nl%remove or delete - self-explanatory%nl%%nl%edit - re-registers a tag, usage just like with 'create' ('title|description')%nl%%nl%image or thumbnail - adds an image or thumbnail to the embed of the specified tag, this is optional and 3rd argument has to be URL pointing to the image%nl%%nl%extra - removes the embed and replies with just plain text, useful for making tags that return only videos or a short string of words```"
			stop
		else if arg-1 is not "create", "delete", "image", "thumbnail", "extra", "remove" or "edit":
			reply to event-message with ":x: **Wrong argument! You can only use ""create"", ""delete"", ""image"", ""thumbnail"", ""extra"" or ""edit""!**"
			stop
		if arg-1 is "create":
			if {discord.tags::*} contains arg-2:
				reply to event-message with ":x: **Tag with that ID already exists! (%arg-2%)**"
				stop
			set {_util::*} to arg-3 split at "|"
			set {discord.tag::%arg-2%::title} to {_util::1}
			set {discord.tag::%arg-2%::text} to {_util::2}
			add arg-2 to {discord.tags::*} 
			reply to event-message with "**Succesfully created tag ""%arg-2%""!**"
		else if arg-1 is "delete" or "remove":
			if {discord.tags::*} doesn't contain arg-2:
				reply to event-message with ":x: **Tag with that ID doesn't exist! (%arg-2%)**"
				stop
			delete {discord.tag::%arg-2%::*}
			remove arg-2 from {discord.tags::*}
			reply to event-message with "**Succesfully removed tag ""%arg-2%""!**"
		else if arg-1 is "edit":
			if {discord.tags::*} doesn't contain arg-2:
				reply to event-message with ":x: **Tag with that ID doesn't exist! (%arg-2%)**"
				stop
			set {_util::*} to arg-3 split at "|"
			set {discord.tag::%arg-2%::title} to {_util::1}
			set {discord.tag::%arg-2%::text} to {_util::2}
			reply to event-message with "**Succesfully edited tag ""%arg-2%""!**"
		else if arg-1 is "image":
			if {discord.tags::*} doesn't contain arg-2:
				reply to event-message with ":x: **Tag with that ID doesn't exist! (%arg-2%)**"
				stop
			set {discord.tag::%arg-2%::image} to arg-3
			reply to event-message with "**Succesfully added image to tag ""%arg-2%""!**"
		else if arg-1 is "thumbnail":
			if {discord.tags::*} doesn't contain arg-2:
				reply to event-message with ":x: **Tag with that ID doesn't exist! (%arg-2%)**"
				stop
			set {discord.tag::%arg-2%::thumbnail} to arg-3
			reply to event-message with "**Succesfully added thumbnail to tag ""%arg-2%""!**"
		else if arg-1 is "extra":
			if {discord.tags::*} doesn't contain arg-2:
				reply to event-message with ":x: **Tag with that ID doesn't exist! (%arg-2%)**"
				stop
			set {discord.tag::%arg-2%::extra} to arg-3
			reply to event-message with "**Succesfully added extra to tag ""%arg-2%""!**"

discord command tag <text>:
	prefixes: !
	aliases: t
	trigger:
		if {discord.tags::*} doesn't contain arg-1:
			reply to event-message with ":x: **Tag with that ID doesn't exist! (%arg-1%)**"
			stop
		if {discord.tag::%arg-1%::extra} is set:
			reply to event-message with "%{discord.tag::%arg-1%::extra}%"
			stop
		make embed:
			set title of embed to "%{discord.tag::%arg-1%::title}%"
			set description of embed to "%{discord.tag::%arg-1%::text}%"
			set image of embed to "%{discord.tag::%arg-1%::image}%" if {discord.tag::%arg-1%::image} is set
			set thumbnail of embed to "%{discord.tag::%arg-1%::thumbnail}%" if {discord.tag::%arg-1%::thumbnail} is set
			set colour of the embed to orange
			set footer of embed to "Requested by %event-user%##%tag of event-user%"
			set footer icon of embed to avatar of event-user
			set timestamp of embed to now
		reply to event-message with last embed