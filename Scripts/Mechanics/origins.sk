# Copyright 2021  FokaStudio

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.


on script load:
	create a gui with id "originGUI" with virtual chest inventory with 5 rows named "&bOrigins":
		format gui slot 0 with player head named "&eHuman" with lore "&7Your normal Minecraft adventure awaits", "", "&fNo perks, but also no debuffs": 
			send "&bYou picked the &eHuman&b origin."
			set {origin::%uuid of player%} to "Human"
			close player's inventory 
		format gui slot 1 with Ender Pearl named "&5Enderian" with lore "&7Born in the End as sons and", "&7daughters of the Ender Dragon,", "&7Enderians are capable of understanding the", "&7ancient teleportation techniques", "", "&aTrue Ender Teleportation", "&fUses less pearls in order to teleport", "&ftakes no teleportation damage", "&fEnder Pearls fly further", "&aDragon's Bless", "&fDeals more damage while in The End", "&aPacifist", "&fEnderman's don't target you no matter what", "&aGraceful Swan", "&fTeleports to ground if the fall distance", "&fis too big", "&aSPEEED!", "&fExtra movement speed", "&cTeleport Limits", "&7Ender Pearl cooldown is doubled", "&cWeakling", "&7Has only 9 Hearts of HP", "&CHydrophobic" and "&7Takes damage in rain and water":
			set walk speed of player to .23
			set max health of player to 9
			send "&bYou picked the &5Enderian&b origin."
			set {origin::%uuid of player%} to "Enderian"
			close player's inventory 
		format gui slot 2 with Honey Bottle named "&eBee" with lore "&7A lone Bee decided to leave the Hive someday,", "&7and began its own adventure...", "", "&aBee Wings", "&fAbility to fly freely", "&aYummy!", "&fSneak + right-click Empty Bottles", "&fto fill them with Honey", "&cWeakling", "&7Only 8 hearts of HP and", "&7low flight speed", "&cHydrophobic", "&7Takes damage in water and rain":
			set flight mode of player to true
			set max health of player to 8
			set the player's flight speed to 0.05
			send "&bYou picked the &eBee&b origin."
			set {origin::%uuid of player%} to "Bee"
			close player's inventory 
#		format gui slot 3 with Feather named "&fAvian" with lore "&aSlow Falling & Speed, &cNo Meat":
#			
#
#			send "&bYou picked the &fAvian&b origin."
#			set {origin::%uuid of player%} to "Avian"
#			close player's inventory 
		format gui slot 4 with Orange Wool named "&6Feline" with lore "&7Some people call them 'Wicked Furries',", "&7but those are actual anthropomorphic entities,", "&7stealthy, quick and silent", "", "&aGraceful Landing", "&fNo fall damage", "&aNutritious!", "&fEvery piece food has increased", "&fsaturation boost", "&aFast and Furious", "&fIncreased Speed", "&aStealthy", "&fMobs are less likely to detect you", "&cWeakling", "&7Only 8 hearts of HP", "&7and permanent mining fatigue":
			set max health of player to 8
			set walk speed of player to .3
			apply potion of mining fatigue without any particles to player for 9999 days
			send "&bYou picked the &6Feline&b origin."
			set {origin::%uuid of player%} to "Feline"
			close player's inventory 
#		format gui slot 5 with Prismarine Shard named "&bGuardian" with lore "&aWater Effects, 25Health, Strength, &cNo Land":
#			
#
#			send "&bYou picked the Guardian &b origin."
#			set {origin::%uuid of player%} to "Guardian"
#			close player's inventory 
#		format gui slot 6 with Phantom Membrane named "&8Phantom" with lore "&a/pm, Invis on sneak, &cDayBurn, 7 Hearts":
#			add "{Purpur.ShouldBurnInDay:1b}" to the nbt of player
#
#			send "&bYou picked the &8Phantom &b origin."
#			set {origin::%uuid of player%} to "Phantom"
#			close player's inventory 
#		format gui slot 7 with tnt named "&aCreeper" with lore "&aExplode on Death & Shift, &cNo Meat":
#			
#
#			send "&bYou picked the &aCreeper &b origin."
#			set {origin::%uuid of player%} to "Creeper" 
#		format gui slot 8 with blaze rod named "&cBlazeborn" with lore "&aNether Fly, Fire Immue, Fire Aspect, &cWater DMG":
#			
#
#			send "&bYou picked the &cBlazeborn &b origin."
#			set {origin::%uuid of player%} to "Blazeborn" 
#			close player's inventory 
#		format gui slot 9 with saddle named "&7Ravager" with lore "&aStrength, Speed, Jump, Rideable, &cCannot Ride Boats and Horses":
#			
#
#			send "&bYou picked the &7Ravager &b origin."
#			set {origin::%uuid of player%} to "Ravager"
#			close player's inventory 
		format gui slot 10 with glass named "&fGlass Golem" with lore "&7Forged in an unknown place,", "&7possibly even in a different reality,", "&7Glass Golem (even if fragile)", "&7is the strongest entity in whole world", "", "&aSuper Strenght", "&fYou are a one-hit anything that sprays", "&fdeath upon those who try to overcome you", "&cFragile", "&7Only 1 heart of HP...":
			set max health of player to 1
			send "&bYou picked the &fGlass Golem &b origin."
			set {origin::%uuid of player%} to "Glass Golem"
			close player's inventory 
		format gui slot 11 with snowball named "&8Snow Golem" with lore "&7The magic that has reanimated those things", "&7and made them able to live among us", "&7is yet to be discovered...", "", "&aFrost Walker", "&fWhen you walk on Water, it turns it", "&finto Ice temporarily", "&aEternal Frost", "&fNo Freezing damage, and also", "&fbonus damage in cold biomes", "&aSnowy Attack!", "&fYou don't use Snowballs on throws,", "&fyou also throw 3 more of them", "&aTAKE THAT!", "&fAll your attacks inflict low-level slowness", "&fand have a chance to temporarily freeze", "&fyour opponents", "&cHeat Intolerancy", "&7Melts in hot biomes", "&cWeakling", "&7Only 9 hearts of HP", "&cHydrophobic" and "&7Takes damage in water and rain":
			set max health of player to 9
			send "&bYou picked the &8Snow Golem &b origin."
			set {origin::%uuid of player%} to "Snow Golem"
			close player's inventory 
		format gui slot 12 with shulker shell named "&5Shulk" with lore "&7The descendant from the End dimension,", "&7Ancient Guardian with unbearable strength,", "&7but also several weaknesses", "", "&aShulker Shell", "&fPermanent Resistance", "&aShulker Storage", "&fSaves armor and hotbar on death,", "&fyou also keep items with", "&fCurse of Vanishing", "&cToo... Heavy...", "&fPermanent Slowness":
			set max health of player to 10
			set player's walk speed to .14
			apply potion of resistance without any particles to player for 9999 days
			send "&bYou picked the &5Shulk &b origin."
			set {origin::%uuid of player%} to "Shulk"
			close player's inventory 
		run on gui close:
			{origin::%uuid of player%} is not set
			cancel gui close

function originAbilities(p: player):
	set {_uuid} to uuid of {_p}
	if {origin::%{_uuid}%} = "Shulk":
		set max health of {_p} to 10
		set {_p}'s walk speed to .14
		apply potion of resistance without any particles to {_p} for 9999 days
	else if {origin::%{_uuid}%} = "Feline":
		set max health of {_p} to 8
		set walk speed of {_p} to .3
		apply potion of mining fatigue without any particles to {_p} for 9999 days
	else if {origin::%{_uuid}%} = "Enderian":
		set walk speed of {_p} to .23
		set max health of {_p} to 9
	else if {origin::%{_uuid}%} = "Bee":
		set flight mode of {_p} to true
		set max health of {_p} to 8
		set flight speed of {_p} to 0.05

on join:
	wait 2 seconds
	{origin::%uuid of player%} is set
	originAbilities(player)

on gamemode change:
	wait 2 seconds
	originAbilities(player)

on respawn:
	wait 2 seconds
	originAbilities(player)

command /rechooseorigin:
	permission: op
	trigger:
		delete {origin::%uuid of player%}
		wait 1 tick
		add "{Purpur.ShouldBurnInDay:0b}" to the nbt of player
		reset player's flight speed
		reset player's walking speed
		reset max health of player
		if player's gamemode is creative or spectator:
			set flight mode of player to true
		else:
			set flight mode of player to false
		console command "effect clear %player%"
		open gui with id "originGUI" to player

command /origingui:
	trigger:
		{origin::%uuid of player%} is not set
		open gui with id "originGUI" to player

on damage of player:
	if {origin::%uuid of victim%} = "Enderian" or "Feline":
		damage cause is fall
		cancel event
	else if {origin::%uuid of victim%} = "Snow Golem":
		"%damage cause%" = "damage causes.freeze"
		cancel event

command /phantommode:
	aliases: pm
	trigger:
		if {origin::%uuid of player%} != "Phantom":
			send "&cOnly Phantom origins have access to that command!"
			stop
		set player's gamemode to spectator
		wait 5 seconds
		
on player world change:
	originAbilities(player)

every 5 seconds:
    originAbilities(all players)

every 10 ticks:
	loop all players where [{origin::%uuid of input%} = "Enderian"]:
		if SKill fall distance of loop-player > 5:
			loop blocks below loop-player:
				if loop-block is not air or cave air:
					teleport loop-player to location 1 meter above location of loop-block
					play sound "entity.enderman.teleport" with volume 1 and pitch 1.15 at loop-player for all players
					play ender signal at loop-player
					set fall distance of loop-player to 0
					exit 1 loop
		if SKill loop-player is wet:
			SKill loop-player doesn't have potion of water breathing
			damage loop-player by 0.25
	loop all players where [{origin::%uuid of input%} = "Snow Golem"]:
		if SKill loop-player is wet:
			SKill loop-player doesn't have potion of water breathing
			damage loop-player by 0.25
		if temperature of block at loop-player >= 1.0:
			SKill loop-player doesn't have potion of fire resistance
			damage loop-player by 0.25
	loop all players where [{origin::%uuid of input%} = "Bee"]:
		SKill loop-player is wet
		SKill loop-player doesn't have potion of water breathing
		damage loop-player by 0.25

on death of player:
	if {origin::%uuid of victim%} = "Enderian":
		damage cause is unknown
		set death message to "%victim% went for a bath as and Enderian"
	else if {origin::%uuid of victim%} = "Snow Golem":
		damage cause is unknown
		set death message to "%victim% melted away"
	else if {origin::%uuid of victim%} = "Bee":
		damage cause is unknown
		set death message to "%victim% tried to take a swim as a Bee"

on damage:
	attacker is player
	if {origin::%uuid of attacker%} = "Glass Golem":
		increase damage by 99999
	else if {origin::%uuid of attacker%} = "Enderian":
		world of attacker is "world_the_end"
		increase damage by 1
	else if {origin::%uuid of attacker%} = "Snow Golem":
		if temperature of block at attacker <= 0:
			increase damage by 2
		apply potion of slowness of tier 1 to victim for 3 seconds
		chance of 15%:
			apply potion of slowness of tier 7 to victim for 7 seconds replacing existing effect
			set {_loc} to location 1.4 meters above victim
			loop 5 times:
				set {_material} to (ice, packed ice, blue ice or snow block)
				play 15 of block particle using {_material} at {_loc}

on right click with ender pearl:
	{origin::%uuid of player%} = "Enderian"
	cooldown of ender pearl for player = 0 seconds
	wait 1 tick
	chance of 75%:
		give player 1 ender pearl
	set the cooldown of ender pearl for player to 2 seconds

on right click with snowball:
	{origin::%uuid of player%} = "Snow Golem"
	give player 1 snowball
	loop 3 times:
		shoot snowball from player with speed 1.5
		wait 1 tick

on right click with empty bottle:
	{origin::%uuid of player%} = "Bee"
	player is sneaking
	remove 1 of player's tool from player
	give player 1 honey bottle
	play sound "item.bottle.fill" at event-player for all players

on entity target:
	event is not canceled
	if {origin::%uuid of target%} = "Enderian":
		event-entity is enderman
		target is a player
		cancel event
	else if {origin::%uuid of target%} = "Feline":
		chance of 50%:
			cancel event

on shoot:
	event is not canceled
	event-projectile is ender pearl
	{origin::%uuid of shooter%} = "Enderian"
	push event-projectile in direction of shooter with speed 0.5

on walk on water:
	{origin::%uuid of player%} = "Snow Golem"
	event-block is water[level=0]
	block 1 meter above event-block isn't water or flowing water
	loop all blocks in radius 1 around player:
		loop-block is water[level=0]
		block 1 meter above loop-block isn't water or flowing water
		set loop-block to frosted_ice[age=0]