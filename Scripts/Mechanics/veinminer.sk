# VEIN MINER by farwl

Options:

# CONFIG

	pickaxe: any ore or copper ore
	axe: any log
	shovel: gravel
	# Set the blocks that will be affected (Mined in veins) by tools above.

	radius: 2
	# how far should veinminer reach

# CONFIG

# CODE #
init:
	delete {pickaxe::*}, {axe::*} and {shovel::*}
	set {_a::*} to all blocks
	loop {_a::*}:
		add loop-value to {pickaxe::*} if loop-value is {@pickaxe}
		add loop-value to {axe::*} if loop-value is {@axe}
		add loop-value to {shovel::*} if loop-value is {@shovel}

command /veinminer:
	aliases: vm
	trigger:
		if player has scoreboard tag "veinminer.on":
			remove "veinminer.on" from scoreboard tags of player
			send "<##FE9D9A>Disabled VeinMiner, you can safely sneak and break blocks now"
		else:
			add "veinminer.on" to the scoreboard tags of player
			send "<##9AFE9C>VeinMiner enabled! Sneak and break a block with right tool to trigger!"

on mine:
	player is sneaking
	player has scoreboard tag "veinminer.on"
	veinMine(event-block, player) if tool is any pickaxe
	veinMine(event-block, player) if tool is any axe
	veinMine(event-block, player) if tool is any shovel
	veinMine(event-block, player) if tool is any hoe

function veinMine(b: block, p: player):
	loop all blocks in radius {@radius} of {_b}:
		set {_fortune} to level of fortune of {_p}'s tool ? 0
		set {_a} to random integer between 0 and {_fortune}
		set {_a::*} to {pickaxe::*} if {_p}'s tool is any pickaxe
		set {_a::*} to {axe::*} if {_p}'s tool is any axe
		set {_a::*} to {shovel::*} if {_p}'s tool is any shovel
		{_a::*} contains loop-block
		break loop-block using {_p}'s tool
		damage tool of {_p} by 1
		if durability of {_p}'s tool = (max durability of {_p}'s tool):
			set {_p}'s tool to air
		else:
			veinMine(loop-block, {_p})
		
